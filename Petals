/* Shama Khalil, 08-01-2018
 *  Code for design project Petals, an interactive music visualizer, desiged
 *  to give music listeners an enhanced music listening experience through
 *  making a visual match a song using through manipulation of the below
 *  listed four parameters of the visual:
 *
 *    Shape change: values[0]
 *    Size change: values[1]
 *    Color change: values[2]
 *    Speed of rotation: values[3] 
 *
 *  These parameters can be manipulated through rotating four rotary potentiometers.
 *  The above listed parameters are each dependent on the values of one
 *  of these potentiometers. Arduino is used to read in value changes of the 
 *  potentiometers and change the parameters of the visual, leading to a change 
 *  in size, color, shape, and motion speed.
 *  For a video demonstration, view (insert youtube link)
*/
import processing.serial.*;

// Global vars for Arduino
Serial myPort;  // Create object from Serial class
int numValues = 4;     // number input values from arduino
float[] values = new float[numValues];
int transparency = 40;

// Global vars for shape variation
float numRotations;
float value;
float speed;

// for changeColor() function
color objectColor;

void setup() {
  fullScreen();
  noStroke();
  // value for size of petal
  value = 1;
  numRotations = 0;
  objectColor = color(0,50,100,40);  // TODODODOODOD TODODOD TODOOODODO TODODOOD CHECK INITIAL VALUE COLOR 

  String portName = Serial.list()[0];
  myPort = new Serial(this, portName, 9600);
  // serialEvents() are only generated if a newline character has been read
  myPort.bufferUntil('\n');
  
  // use HSB to make sure colors never clash
  colorMode(HSB,100,100,100);
}

int cnt = 0;

void draw() {
  cnt+=speed;
  background(0, 0, 0);
  fill(objectColor);
  // if all values from the newline have successfully been received from Arduino
  if (values.length >= numValues) {  
    changeSize();
    changeSpeed();
    changeShape();
    changeColor();
  }
  translate(width/2, height/2);
  for (int q = 0; q < 360; q+=30) {
    // Manipulate the spread of the petals, leading to a shapechange
    rotate(radians(numRotations));
    beginShape();
    for (int i = -90; i <= 90; i+=3) {
      // default value for angle 
      float angle = sin(radians(90+i))*value;
      float x = sin(radians(i*3+cnt))*(angle);
      float y = -i*2;
      vertex(x, y);
    }
    endShape();
  }
  ++cnt;
}


void changeSize() {
  value = values[1];
}

void changeSpeed() {
  speed = values[3]/40.;
}

void changeShape() {
  numRotations = values[0]/65.;
}

void changeColor() {
  int hue = (int)map(values[2],0,1024,0,100);
  println(hue);
  objectColor = color(hue,50,100,40);
}


void serialEvent(Serial myPort) { 
  try {
    // Read data from arduino Serial
    String inString = myPort.readStringUntil('\n');
    // println("raw: \t" + inString); // <- uncomment this to debug serial input from Arduino
    if (inString != null) {

      // trim off any whitespace:
      inString = trim(inString);

      // split the string on the delimiters and convert the resulting substrings into an float array:
      values = float(splitTokens(inString, ", \t")); // delimiter can be comma space or tab
      
      // if the array has at least the # of elements as your # of sensors, you know
      //   you got the whole data packet.
      if (values.length >= numValues) {
        //println(values[0] + ", " + values[1] + ", " + values[2]);
      }
    }
  }
  catch(RuntimeException e) {
    // only if there is an error:
    e.printStackTrace();
  }
}
